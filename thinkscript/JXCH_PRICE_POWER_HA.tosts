# 
# 根据平均K线计算的 OBV FI DMI 能量指标，给K线上色
#

input OBV_Length = 144;
input FI_Length = 13;
input DMI_length = 13;
input DMI_averageType = AverageType.WILDERS;

def haClose = (open+high+low+close)/4;
def haopen = CompoundValue(1,(haopen[1]+haclose[1])/2, (open[1]+haClose[1])/2);
def haHigh = Max(high, Max(haOpen, haClose));
def haLow = Min(low, Min(haOpen, haClose));

def OBV = TotalSum(Sign(haClose - haClose[1]) * volume);
def OBV_EMA = ExpAverage(OBV, OBV_Length);
def FI = ExpAverage(data = (haClose - haClose[1]) * volume, FI_Length);

def hiDiff = haHigh - haHigh[1];
def loDiff = haLow[1] - haLow;
def plusDM = if hiDiff > loDiff and hiDiff > 0 then hiDiff else 0;
def minusDM =  if loDiff > hiDiff and loDiff > 0 then loDiff else 0;
def ATR = MovingAverage(DMI_averageType, TrueRange(haHigh, haClose, haLow), DMI_length);
def "DI+" = 100 * MovingAverage(DMI_averageType, plusDM, DMI_length) / ATR;
def "DI-" = 100 * MovingAverage(DMI_averageType, minusDM, DMI_length) / ATR;
def DX = if ("DI+" + "DI-" > 0) then 100 * AbsValue("DI+" - "DI-") / ("DI+" + "DI-") else 0;
def ADX = MovingAverage(DMI_averageType, DX, DMI_length);

AssignPriceColor(if OBV > OBV_EMA and FI > 0 and "DI+" > "DI-" then Color.GREEN else if OBV < OBV_EMA and FI < 0 and "DI+" < "DI-" and ADX > 10 then Color.RED else Color.GRAY);