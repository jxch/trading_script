# 
# 根据平均K线计算DMI指标
#

declare lower;

input length = 13;
input averageType = AverageType.WILDERS;

def haClose = (open + high + low + close) / 4;
def haopen = CompoundValue(1,(haopen[1]+haclose[1])/2, (open[1]+haClose[1])/2);
def haHigh = Max(high, Max(haOpen, haClose));
def haLow = Min(low, Min(haOpen, haClose));

def hiDiff = haHigh - haHigh[1];
def loDiff = haLow[1] - haLow;

def plusDM = if hiDiff > loDiff and hiDiff > 0 then hiDiff else 0;
def minusDM =  if loDiff > hiDiff and loDiff > 0 then loDiff else 0;

def ATR = MovingAverage(averageType, TrueRange(haHigh, haClose, haLow), length);
plot "DI+" = 100 * MovingAverage(averageType, plusDM, length) / ATR;
plot "DI-" = 100 * MovingAverage(averageType, minusDM, length) / ATR;

def DX = if ("DI+" + "DI-" > 0) then 100 * AbsValue("DI+" - "DI-") / ("DI+" + "DI-") else 0;
plot ADX = MovingAverage(averageType, DX, length);

"DI+".SetDefaultColor(Color.DARK_GREEN);
"DI-".SetDefaultColor(Color.RED);
ADX.SetDefaultColor(Color.BLACK);

AddCloud("DI+", "DI-", Color.GREEN, Color.PINK);